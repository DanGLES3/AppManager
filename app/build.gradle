// SPDX-License-Identifier: Apache-2.0 AND GPL-3.0-or-later

apply plugin: 'com.android.application'
apply plugin: 'dev.rikka.tools.refine.gradle-plugin'

android {
    compileSdkVersion rootProject.ext.compileSdk
    buildToolsVersion rootProject.ext.buildTools

    defaultConfig {
        applicationId 'io.github.muntashirakon.AppManager'
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode 403
        versionName "3.0.0-rc01"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file('dev_keystore.jks')
            storePassword 'kJCp!Bda#PBdN2RLK%yMK@hatq&69E'
            keyPassword 'kJCp!Bda#PBdN2RLK%yMK@hatq&69E'
            keyAlias 'key0'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "App Manager"
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            signingConfig signingConfigs.debug
            resValue "string", "app_name", "AM Debug"
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
    splits {
        abi {
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
    aaptOptions {
        noCompress 'jar', 'sh'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    compileOnly project(path: ':hiddenapi')

    implementation project(path: ':libapkparser')
    implementation project(path: ':libcore:io')
    implementation project(path: ':libcore:ui')
    implementation project(path: ':libopenpgp')
    implementation project(path: ':libserver')
    implementation project(path: ':docs')

    implementation 'com.github.MuntashirAkon:apksig-android:178a05e4c9'
    implementation 'com.github.MuntashirAkon:unapkm-android:1.2'
    implementation 'com.github.MuntashirAkon:sun-security-android:1.1'
    implementation 'com.github.MuntashirAkon:libadb-android:2.2.0'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.69'
    implementation 'org.smali:baksmali:2.5.2'
    implementation 'org.smali:smali:2.5.2'
    implementation 'com.github.MuntashirAkon.jadx:jadx-core:cb8c1ddc7f'
    implementation 'com.github.MuntashirAkon.jadx:jadx-dex-input:cb8c1ddc7f'

    implementation "com.google.android.material:material:${material_version}"
    implementation "androidx.appcompat:appcompat:${appcompat_version}"
    // AppCompat 1.4.1 still includes the buggy implementation of documentfile library (1.0.0)
    implementation "androidx.documentfile:documentfile:1.0.1"
    implementation "androidx.core:core-splashscreen:${splashscreen_version}"
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.webkit:webkit:1.4.0'
    implementation "androidx.room:room-runtime:${room_version}"
    annotationProcessor "androidx.room:room-compiler:${room_version}"
    implementation 'com.google.code.gson:gson:2.9.0'

    implementation "com.github.YarikSOffice:lingver:1.3.0"

    implementation "com.github.topjohnwu.libsu:core:${libsu_version}"

    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
    implementation "dev.rikka.tools.refine:runtime:${refine_version}"

    implementation 'me.zhanghai.android.fastscroll:library:1.1.7'
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'

    // Espresso UI Testing
//    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
    // Optional if you need to detect intents.
//    androidTestImplementation "com.android.support.test.espresso:espresso-intents:3.0.2"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.8.1'
}

preBuild.dependsOn ":server:build"
